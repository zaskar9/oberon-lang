(*
   The Syntax of Oberon-0
   Niklaus Wirth, May 2017
*)

Letter = "a"|"b"|"c"|"d"|"e"|"f"|"g"|"h"|"i"|"j"|"k"|"l"|"m"|"n"|"o"|"p"|"q"|"r"|"s"|"t"|"u"|"v"|"w"|"x"|"y"|"z"|"A"|"B"|"C"|"D"|"E"|"F"|"G"|"H"|"I"|"J"|"K"|"L"|"M"|"N"|"O"|"P"|"Q"|"R"|"S"|"T"|"U"|"V"|"W"|"X"|"Y"|"Z" .
Digit = "0"|"1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9" .

Ident = Letter { Letter | Digit } .
Integer = Digit { Digit } .

Selector = { "." Ident | "[" Expression "]" } .
Factor = Ident Selector | Integer | "(" Expression ")" | "~" Factor .
Term = Factor { ( "*" | "DIV" | "MOD" | "&" ) Factor } .
SimpleExpression = [ "+" | "-" ] Ident { ( "+" | "-" | "OR" ) Ident } .
Expression = SimpleExpression [ ( "=" | "#" | "<" | "<=" | ">" | ">=") SimpleExpression ] .

Assignment = Ident Selector ":=" Expression .
ActualParameters = "(" [ Expression { "," Expression } ] ")" .
ProcedureCall = Ident [ ActualParameters ] .
IfStatement = "IF" Expression "THEN" StatementSequence
              { "ELSIF" Expression "THEN" StatementSequence }
              [ "ELSE" StatementSequence ] "END" .
WhileStatement = "WHILE" Expression "DO" StatementSequence "END" .
Statement = [ Assignment | ProcedureCall | IfStatement | WhileStatement ] .
StatementSequence = Statement { ";" Statement } .

IdentList = Ident { "," Ident } .
ArrayType = "ARRAY" Expression "OF" Type .
FieldList = [ IdentList ":" Type ] .
RecordType = "RECORD" FieldList { ";" FieldList } "END" .
Type = Ident | ArrayType | RecordType .
FPSection = [ "VAR" ] IdentList ":" Type .
FormalParameters = "(" [ FPSection { ";" FPSection } ] ")" .
ProcedureHeading = "PROCEDURE" Ident [ FormalParameters ] .
ProcedureBody = Declarations [ "BEGIN" StatementSequence ] "END" .
ProcedureDeclaration = ProcedureHeading ";" ProcedureBody Ident .
Declarations = [ "CONST" { Ident "=" Expression ";" } ]
               [ "TYPE" { Ident "=" Type ";" } ]
               [ "VAR" { IdentList ":" Type ";" } ]
               { ProcedureDeclaration ";" } .

Module* = "MODULE" Ident ";" Declarations [ "BEGIN" StatementSequence ] "END" Ident "." .